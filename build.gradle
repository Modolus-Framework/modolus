plugins {
    id 'java'
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.36.0'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.diffplug.spotless' version '8.1.0'
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://nexus.lucko.me/repository/maven-hytale/' }
    }
}

def releaseType = System.getenv("RELEASE_TYPE")
def isSnapshot = releaseType == null || releaseType != "RELEASE"

def baseVersion = '0.0.2'
def projectVersion = isSnapshot ? "$baseVersion-SNAPSHOT" : baseVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    group = 'dev.modolus'
    version = projectVersion

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            cleanthat()

            googleJavaFormat()
            formatAnnotations()

            licenseHeaderFile(rootProject.file('required-file-header.txt'))
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compileOnly 'com.hypixel.hytale:HytaleServer:2026.01.17-4b0f30090-SNAPSHOT'

        implementation "org.jetbrains:annotations:26.0.2"
        compileOnly "org.projectlombok:lombok:1.18.42"
        annotationProcessor "org.projectlombok:lombok:1.18.42"

        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testImplementation 'com.hypixel.hytale:HytaleServer:2026.01.17-4b0f30090-SNAPSHOT'
        testCompileOnly "org.projectlombok:lombok:1.18.42"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.42"
    }

    test {
        useJUnitPlatform()
    }
}

def publishedModules = [
        ":modolus-annotations",
        ":modolus-core",
        ":modolus-processor",
        ":modolus-util"
]

subprojects { sub ->
    {
        if (publishedModules.contains(sub.path)) {
            apply plugin: 'com.vanniktech.maven.publish'

            mavenPublishing {
                coordinates(sub.group.toString(), sub.name, sub.version.toString())

                publishToMavenCentral()
                signAllPublications()

                pom {
                    name = sub.name
                    description = sub.description
                    url = "https://github.com/Modolus-Framework/modolus"
                    licenses {
                        license {
                            name = "GNU General Public License v3.0"
                            url = "https://www.gnu.org/licenses/gpl-3.0.txt"
                            distribution = ""
                        }
                    }

                    developers {
                        developer {
                            name = "Louis Schmieder"
                            email = "dev@louis-schmieder.de"
                            organization {
                                name = "Modolus-Framework"
                                url = "https://github.com/Modolus-Framework"
                            }
                        }
                    }

                    scm {
                        url = "https://github.com/Modolus-Framework/modolus"
                        connection = "scm:git:git://github.com/Modolus-Framework/modolus.git"
                        developerConnection = "scm:git:ssh://git@github.com/Modolus-Framework/modolus.git"
                    }
                }
            }

        }
    }
}

def hytaleAssetsPath = getHytaleAssetsPath(file("hytale.properties"))

dependencies {
    implementation 'com.hypixel.hytale:HytaleServer:2026.01.17-4b0f30090-SNAPSHOT'
    implementation project(":modolus-core")
    implementation project(":modolus-test-plugin")
}

def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) serverRunDir.mkdirs()

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = rootProject.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=$hytaleAssetsPath --mods=${file("src/main/").absolutePath}  --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}

static GString getHytaleAssetsPath(propertiesFile) {
    if (System.getenv("SKIP_SERVER_TASK") != null) return GString.EMPTY

    if (!propertiesFile.exists()) {
        if (!propertiesFile.createNewFile()) {
            throw new GradleException("Failed to create hytale.properties")
        }

        def defaultProperties = new Properties()
        defaultProperties.setProperty("hytale.home", "")
        defaultProperties.setProperty("hytale.patch-line", "release")
        defaultProperties.setProperty("hytale.build", "latest")

        propertiesFile.withOutputStream { defaultProperties.store(it, "Hytale properties") }
        throw new GradleException("Set hytale.home in your hytale.properties")
    }

    def hytaleProperties = new Properties()
    propertiesFile.withInputStream { hytaleProperties.load(it) }

    def hytaleHome = hytaleProperties.get('hytale.home')
    def hytalePatchLine = hytaleProperties.get('hytale.patch-line')
    def hytaleBuild = hytaleProperties.get('hytale.build')

    return "$hytaleHome/install/$hytalePatchLine/package/game/$hytaleBuild/Assets.zip"
}

