plugins {
    id 'java'
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.36.0'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.diffplug.spotless' version '8.1.0'
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://nexus.lucko.me/repository/maven-hytale/' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    group = 'dev.modolus'
    version = '0.0.1'

    java {
        withSourcesJar()
        withJavadocJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            cleanthat()

            googleJavaFormat()
            formatAnnotations()

//            licenseHeaderFile(rootProject.file('LICENSE-HEADER'))
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compileOnly 'com.hypixel.hytale:HytaleServer:2026.01.13-dcad8778f-SNAPSHOT'

        implementation "org.jetbrains:annotations:26.0.2"
        compileOnly "org.projectlombok:lombok:1.18.42"
        annotationProcessor "org.projectlombok:lombok:1.18.42"

        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testImplementation 'com.hypixel.hytale:HytaleServer:2026.01.13-dcad8778f-SNAPSHOT'
        testCompileOnly "org.projectlombok:lombok:1.18.42"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.42"
    }

    test {
        useJUnitPlatform()
    }
}

def publishedModules = [
        ":modolus-annotations",
        ":modolus-core",
        ":modolus-processor",
        ":modolus-util"
]

subprojects { sub ->
    {
        if (publishedModules.contains(sub.path)) {
            apply plugin: 'com.vanniktech.maven.publish'

            mavenPublishing {
                publishToMavenCentral()
                signAllPublications()

                pom {
                    name = sub.getProperties().get("library-name")
                    description = sub.getProperties().get("library-description")
                    url = "https://github.com/Modolus-Framework/modolus"
                    licenses {
                        license {
                            name = "GNU General Public License v3.0"
                        }
                    }
                }
            }

        }
    }
}

def hytaleServerPath = getHytaleServerPath(file("hytale.properties"))
def hytaleAssetsPath = getHytaleAssetsPath(file("hytale.properties"))

dependencies {
    implementation files(hytaleServerPath)
    implementation project(":modolus-core")
    implementation project(":modolus-test-plugin")
}

def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) serverRunDir.mkdirs()

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = rootProject.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=$hytaleAssetsPath --mods=${file("src/main/").absolutePath}  --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}

static GString getHytaleServerPath(propertiesFile) {
    if (System.getenv("SKIP_SERVER_TASK") != null) return GString.EMPTY

    if (!propertiesFile.exists()) {
        if (!propertiesFile.createNewFile()) {
            throw new GradleException("Failed to create hytale.properties")
        }

        def defaultProperties = new Properties()
        defaultProperties.setProperty("hytale.home", "")
        defaultProperties.setProperty("hytale.patch-line", "release")
        defaultProperties.setProperty("hytale.build", "latest")

        propertiesFile.withOutputStream { defaultProperties.store(it, "Hytale properties") }
        throw new GradleException("Set hytale.home in your hytale.properties")
    }

    def hytaleProperties = new Properties()
    propertiesFile.withInputStream { hytaleProperties.load(it) }

    def hytaleHome = hytaleProperties.get('hytale.home')
    def hytalePatchLine = hytaleProperties.get('hytale.patch-line')
    def hytaleBuild = hytaleProperties.get('hytale.build')

    return "$hytaleHome/install/$hytalePatchLine/package/game/$hytaleBuild/Server/HytaleServer.jar"
}

static GString getHytaleAssetsPath(propertiesFile) {
    if (System.getenv("SKIP_SERVER_TASK") != null) return GString.EMPTY

    if (!propertiesFile.exists()) {
        if (!propertiesFile.createNewFile()) {
            throw new GradleException("Failed to create hytale.properties")
        }

        def defaultProperties = new Properties()
        defaultProperties.setProperty("hytale.home", "")
        defaultProperties.setProperty("hytale.patch-line", "release")
        defaultProperties.setProperty("hytale.build", "latest")

        propertiesFile.withOutputStream { defaultProperties.store(it, "Hytale properties") }
        throw new GradleException("Set hytale.home in your hytale.properties")
    }

    def hytaleProperties = new Properties()
    propertiesFile.withInputStream { hytaleProperties.load(it) }

    def hytaleHome = hytaleProperties.get('hytale.home')
    def hytalePatchLine = hytaleProperties.get('hytale.patch-line')
    def hytaleBuild = hytaleProperties.get('hytale.build')

    return "$hytaleHome/install/$hytalePatchLine/package/game/$hytaleBuild/Assets.zip"
}

